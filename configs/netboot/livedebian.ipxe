#!ipxe

# Debian Live
# https://www.debian.org/devel/debian-live/

goto ${menu}

:livedebian
set os Debian Live
menu ${os}
item --gap Versions
item 8.9.0 ${space} ${os} 8.9.0
choose livedebian_version || goto livedebian_exit

:livedebian_flavor
menu ${os} ${livedebian_version}
item --gap Flavors
item cinnamon-desktop ${space} ${livedebian_version} Cinnamon
item gnome-desktop ${space} ${livedebian_version} Gnome
item kde-desktop ${space} ${livedebian_version} KDE
item lxde-desktop ${space} ${livedebian_version} LXDE
item mate-desktop ${space} ${livedebian_version} MATE
item standard ${space} ${livedebian_version} Standard
item xfce-desktop ${space} ${livedebian_version} XFCE
choose --default ${type} flavor || goto livedebian
echo ${cls}
goto livedebian_boot

:livedebian_boot
set webboot_host cdimage.debian.org
set livedebian_url http://${webboot_host}/cdimage/archive/${livedebian_version}-live/amd64/webboot/debian-live-${livedebian_version}-amd64-${flavor}
imgfree
kernel ${livedebian_url}.vmlinuz boot=live config hooks=filesystem username=live noeject ${console} fetch=${livedebian_url}.squashfs initrd=debian-live-${livedebian_version}-amd64-${flavor}.initrd.img
module ${livedebian_url}.initrd.img
# used to resolve DNS
module http://${boot_domain}/live-helpers/debian/libresolv.so.2 /lib/libresolv.so.2
module http://${boot_domain}/live-helpers/debian/libnss_dns.so.2 /lib/libnss_dns.so.2
boot
goto livedebian_exit

:livedebian_exit
clear menu
exit 0
