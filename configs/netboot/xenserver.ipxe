#!ipxe

# Citrix XenServer Hypervisor
# http://xenserver.org

goto ${menu} ||

:xenserver
clear osversion
set os Citrix XenServer
menu ${os} 
item --gap Stable Releases
item 7.0.0 ${space} ${os} 7.0 (dundee)
item 6.5.0 ${space} ${os} 6.5 (creedence)
choose xs_version || goto hypervisor_menu
goto determine_type

:determine_type
set xs_mirror downloadns.citrix.com.edgesuite.net/11419/pxe
set xs_type release/${xs_version}
goto boot_xs

:boot_xs
imgfree
kernel http://${xs_mirror}/${xs_type}/boot/xen dom0_max_vcpus=1-2 dom0_mem=752M,max:752M com1=115200,8n1 console=com1,vga
module http://${xs_mirror}/${xs_type}/boot/vmlinuz xencons=hvc console=hvc0 console=tty0 install 
module http://${xs_mirror}/${xs_type}/install.img
echo
echo MD5sums:
md5sum xen vmlinuz install.img
iseq ${img_sigs_enabled} true && goto verify_sigs || goto skip_sigs
:verify_sigs
echo
echo Checking signatures...
imgverify xen ${sigs}xenserver/11419/pxe/release/${xs_version}/boot/xen.sig || goto error
imgverify vmlinuz ${sigs}xenserver/11419/pxe/release/${xs_version}/boot/vmlinuz.sig || goto error
imgverify install.img ${sigs}xenserver/11419/pxe/release/${xs_version}/install.img.sig || goto error
echo Signatures verified!
echo
:skip_sigs
echo When prompted for the repo to install from,
echo choose http and use the following repo:
echo
echo http://${xs_mirror}/${xs_type}
echo
prompt Press any key to continue...
boot
goto hypervisor_menu

:hypervisor_menu
clear menu
exit 0
